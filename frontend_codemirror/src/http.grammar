@top Request {
  HttpPart
  separator
  Body
}

HttpPart {
  Method " " Path " " Version "\n"
  Headers
}

Method { method }

Version { version }

Headers { (Header "\n")* }

Header { HeaderKey ": " HeaderValue }

HeaderKey { keyVal }

HeaderValue { keyVal }

Path { noSpaceLine }

Body { line? ("\n"+ line)* }

@tokens {
    method { "GET" | "POST" | "PUT" | "DELETE" | "PATCH" }
    version { "HTTP/" ( ( @digit "." @digit ) | @digit ) }
    separator { '---' '-'* }
    noSpaceLine { ![ \n]+ }
    line { ![\n]+ }
    keyVal { ![-:\n] ![:\n]* }
}

@external propSource httpHighlighting from "./highlight"

@skip {}